<html>
  <head>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/paho-mqtt/1.0.1/mqttws31.min.js" type="application/javascript" async="false"></script>
    <script src="https://uix-playground.vercel.app/assets/tailwind.js" type="application/javascript"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.js" type="application/javascript"></script>
    <script crossorigin src="https://unpkg.com/react@17/umd/react.production.min.js"></script>
    <script crossorigin src="https://unpkg.com/react-dom@17/umd/react-dom.production.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/scittle@0.6.15/dist/scittle.reagent.js" type="application/javascript"></script>
    <script type="application/x-scittle">
        (require
         '[clojure.string :as str]
         '[reagent.core :as rcore]
         '[reagent.dom :as rdom]
         '[reagent.ratom :as ratom])
       
      ;; -----------------------------------------------------------------------------------
      ;; Datenmodell https://reagent-project.github.io/docs/master/reagent.ratom.html#var-atom
      ;; -----------------------------------------------------------------------------------
      
      (defonce data
        (ratom/atom {}))
      
      (defn set-my-name! [evt]
        (swap! data update-in [:my-name] (constantly (-> evt .-target .-value))))
      
      (defn set-message! [evt]
        (swap! data update-in [:message] (constantly (-> evt .-target .-value))))
      
      (defn clear-message! []
        (swap! data update-in [:message] (constantly "")))
      
      (defn append-to-messages! [text]
        (swap! data update-in [:messages] str text "\n"))
      
      (defn clear-messages! []
        (swap! data update-in [:messages] (constantly "")))
      
      ;; -----------------------------------------------------------------------------------
      ;; MQTT https://mqtt.org/ JavaScript Anbindung https://github.com/eclipse/paho.mqtt.javascript
      ;; -----------------------------------------------------------------------------------
      
      (defonce load-script
        (js/document.body.appendChild
         (doto (js/document.createElement "script")
           (.setAttribute "src"
                          "https://cdnjs.cloudflare.com/ajax/libs/paho-mqtt/1.0.1/mqttws31.min.js"))))
      
      ;; https://iot.eclipse.org/projects/sandboxes/
      (def mqtt-uri "wss://mqtt.eclipseprojects.io/mqtt")
      (def my-client-id (str (random-uuid)))
      
      (def topic "our-topic-put-your-topic-here")
      
      (declare client)  
      
      (defn on-connect []
        (println "*** connected!")
        (.subscribe client topic)
        (println "*** subscibed!"))
      
      (defn on-message-arrived [msg]
        (let [text (aget msg "payloadString")]
          (append-to-messages! text)
          (println "*** received from" text)))
      
      (defonce client
        (doto (js/Paho.MQTT.Client. mqtt-uri my-client-id)
          (.connect (clj->js {:onSuccess on-connect}))
          (aset "onMessageArrived" #(on-message-arrived %))))
      
      (defn publish [my-name text]
        (and (not (str/blank? my-name)) (not (str/blank? text))
             (if-not (.isConnected client)
               (do
                 (println (str "*** delay '" text "' because not connected yet!"))
                 (js/setTimeout #(publish my-name text) 1000))
               (let [msg (js/Paho.MQTT.Message. (str my-name ": " text))]
                 (aset msg "destinationName" topic)
                 (.send client msg)))))
      
      #_(publish my-client-id "JOINED!")
      
      ;; -----------------------------------------------------------------------------------
      ;; GUI Elemente
      ;; -----------------------------------------------------------------------------------
      
      ;; Tailwind CSS Styling https://tailwindcss.com/docs/installation
      (def button-class
        (str " m-1 p-2 bg-violet-500"
             " border rounded-full"
             " focus:outline-none focus:ring focus:ring-violet-300"
             " hover:bg-violet-600 active:bg-violet-700"))
      
      (def text-input-class
        (str " m-1 p-2 bg-gray-200 text-gray-900 text-sm rounded-lg"
             " border border-gray-500"
             " focus:outline-none focus:ring focus:ring-violet-400 focus:border-violet-400"
             #_" block w-full"))
      
      ;; Reagent Komponenten https://reagent-project.github.io/
      (defn watch-data []
        [:p
         (str @data)])
      
      (defn text-area []
        [:textarea#messages {:value (:messages @data)
                             :placeholder "Nachrichten"
                             :class (str " m-2 p-2 rounded-lg bg-gray-200 text-sm text-blue-gray-700"
                                         " border border-blue-900"
                                         " h-full resize-none")}])
      
      ;; Auto-scroll 
      (add-watch data :text-area
                 (fn [a-key the-ref old-state new-state]
                   (when (not= (:messages old-state) (:messages new-state))
                     (when-let [e (.getElementById js/document "messages")]
                       (set! (.-scrollTop e) (.-scrollHeight e))))))
      
      (defn set-focus! []
        (-> js/document (.getElementById "message") .focus))
      
      (defn my-name-input []
        [:input {:type :text
                 :class text-input-class
                 :placeholder "Dein Name"
                 :value (:my-name @data)
                 :on-change #(set-my-name! %)}])
      
      (defn message-input []
        [:input#message {:type :text
                         :class text-input-class
                         :placeholder "Deine Nachricht"
                         :value (:message @data)
                         :on-change #(set-message! %)
                         ;; Default-Button
                         :on-key-press #(when (= (.-key %) "Enter")
                                          (-> js/document (.getElementById "send") .click))}])
      
      (defn send-button []
        [:button#send {:class button-class
                       :on-click #(do (publish (:my-name @data) (:message @data))
                                      (clear-message!)
                                      (set-focus!))}
         "Senden!"])
      
      (defn clear-button []
        [:button {:class button-class
                  :on-click #(do (clear-messages!) (set-focus!))}
         "LÃ¶schen!"])
      
      ;; -----------------------------------------------------------------------------------
      ;; Die "App"
      ;; -----------------------------------------------------------------------------------
      
      (defn app []
        [:div {:class "h-screen flex flex-col w-full bg-blue-200"}
         [:div
          #_[watch-data]
          [my-name-input]
          [message-input]
          [send-button]
          [clear-button]]
         [text-area]])
      
      (rdom/render app (.getElementById js/document "root"))
    </script>
  </head>
  <body>
    <div id="root"></div>
  </body>
</html>